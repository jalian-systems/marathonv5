/*
Copyright 2016 Jalian Systems Pvt. Ltd.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

repositories {
  mavenCentral()
}

configurations {
  antBuildXML
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

eclipse {
  jdt {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
    javaRuntimeName = 'JavaSE-1.6'

    file {
      withProperties { properties ->
        properties.setProperty('org.eclipse.jdt.core.compiler.problem.discouragedReference', 'ignore')
        properties.setProperty('org.eclipse.jdt.core.compiler.problem.forbiddenReference', 'ignore')
      }
    }
  }
}

compileJava {
	options.fork = true
	if(project.properties['jdk6home'])
		options.forkOptions*.executable = project.properties['jdk6home'] + '/bin/javac'
}

compileTestJava {
	options.fork = true
	if(project.properties['jdk6home'])
		options.forkOptions*.executable = project.properties['jdk6home'] + '/bin/javac'
}

dependencies {
  compile project(":marathon-core:JSON-java")
  compile project(':marathon-java:marathon-java-agent')
  compile project(":marathon-core:nanohttpd-2.1.1")
  testCompile "org.testng:testng:${testng_version}"
  testCompile project(":marathon-java:marathon-test-helpers")
  testCompile project(':marathon-java:marathon-java-agent')
  antBuildXML project(":marathon-core:JSON-java").files("bin")
  antBuildXML project(':marathon-java:marathon-java-agent').files("bin")
  antBuildXML project(":marathon-core:nanohttpd-2.1.1").files("bin")
}

jar {
  configurations.compile.each { from(zipTree(it.path).matching { exclude("META-INF/**") }) }
  manifest {
    attributes 'Manifest-Version': '1.0',
      'Agent-Class': 'net.sourceforge.marathon.javarecorder.JavaRecorderHook',
      'Can-Redefine-Classes': 'false',
      'Can-Retransform-Classes': 'false',
      'Premain-Class': 'net.sourceforge.marathon.javarecorder.JavaRecorderHook'
  }
}

test {
  useTestNG()
}
